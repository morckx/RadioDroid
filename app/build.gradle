apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "de.undercouch.download"

buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "de.undercouch:gradle-download-task:5.4.0"
    }
}


def getAvailableLocales() {
    def tree = fileTree(dir: 'src/main/res', include: '**/strings.xml')
    return tree.collect {
        def lang = it.getParentFile().getName() - "values-" - "values"

        // We want a name which would be understood by Locale::forLanguageTag, so we should do
        // do a simple conversion for an edge case:
        //   "pt-rBR" -> "pt-BR"
        //   "zh-rCN" -> "zh-CN"
        //   and so on
        lang = lang.replace("-r", "-")

        if (lang.empty) {
            lang = "en"
        }

        lang
    }
}

android {
    flavorDimensions "one"
    compileSdkVersion 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of("17"))
        }
    }

    buildFeatures {
        viewBinding true
        aidl true
        buildConfig true
    }

    defaultConfig {
        applicationId "net.programmierecke.radiodroid2"
        minSdkVersion 21
        targetSdkVersion 35

        versionCode 97
        versionName "0.86.900-morckx"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        // Define your application name here.
        // It must neither be present in /res/values/strings.xml
        // nor in /res/values/string_no_translate.xml
        resValue 'string', 'app_name_untranslated', 'RadioDroid'

        // There is no easy way to get available locales during runtime, and especially before
        // tests setup.
        def escapedLocales = getAvailableLocales().collect { "\"" + it + "\"" }
        buildConfigField "String[]", "AVAILABLE_LOCALES", String.format("{ %s }", escapedLocales.join(","))

        testInstrumentationRunner "net.programmierecke.radiodroid2.tests.CustomTestRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        // buildConfigField "java.util.concurrent.atomic.AtomicBoolean", "IS_TESTING", "new java.util.concurrent.atomic.AtomicBoolean(false)"
        buildConfigField "boolean", "IS_TESTING", "false"

        multiDexKeepProguard file('multidex-config.pro')

        vectorDrawables.useSupportLibrary = true
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    sourceSets {
        // Store flags in subfolder to not pollute drawables
        // https://antonioleiva.com/android-multiple-resource-folders/
        main.res.srcDirs += ["src/main/res/drawable/flags"]

        androidTest {
            resources.srcDirs += ['src/androidTest/resources']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            lintOptions {
                disable 'MissingTranslation'
            }
        }
    }
    productFlavors {
        play {
            dimension "one"
        }
        free {
            dimension "one"
        }
    }
    lint {
        abortOnError true
        disable 'UnsafeOptInUsageError'
    }
    namespace 'net.programmierecke.radiodroid2'

    testOptions {
        unitTests.all {
            useJUnit()
        }
        animationsDisabled = true
        unitTests.returnDefaultValues = true
    }

    task renewFallbackStations(type: Download) {
        group 'Build Setup'
        description 'Renews the list of some popular and recently checked fallback stations'
        src 'https://de1.api.radio-browser.info/json/stations/search?limit=10&bitrateMax=128&hidebroken=true&has_extended_info=true&order=lastchecktime&reverse=true&bitrateMax=128'
        dest new File("app/src/main/res/raw", "fallback_stations.json")
        overwrite true
        tempAndMove true
    }
}

ext {
    media3Version = '1.7.1'
    iconicsVersion = '4.0.2'
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.mediarouter:mediarouter:1.8.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.tvprovider:tvprovider:1.1.0'

    // Keep OkHttp 3.12.X to support Android 4.X, see https://developer.squareup.com/blog/okhttp-3-13-requires-android-5
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.13'

    //noinspection GradleDependency
    implementation 'com.google.code.gson:gson:2.9.1'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
    implementation "androidx.media3:media3-exoplayer:$media3Version"
    implementation "androidx.media3:media3-exoplayer-hls:$media3Version"
    implementation "com.mikepenz:iconics-core:$iconicsVersion"
    implementation "com.mikepenz:iconics-views:$iconicsVersion"
    implementation 'com.mikepenz:google-material-typeface:3.0.1.4.original-kotlin@aar'
    implementation 'com.mikepenz:community-material-typeface:3.7.95.4-kotlin@aar'
    implementation 'com.github.rustamg:file-dialogs:1.0'
    implementation 'info.debatty:java-string-similarity:2.0.0'
    implementation 'me.xdrop:fuzzywuzzy:1.4.0'
    implementation 'com.github.zawadz88:MaterialPopupMenu:3.4.0'
    implementation 'com.github.ByteHamster:SearchPreference:v2.3.0'

    implementation 'androidx.room:room-runtime:2.7.1'
    androidTestImplementation("androidx.test.espresso:espresso-contrib:3.6.1") {
        exclude group: 'org.checkerframework', module: 'checker'
    }
    annotationProcessor 'androidx.room:room-compiler:2.7.1'

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.9.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.1"

    implementation 'androidx.paging:paging-runtime-ktx:3.3.6'

    playImplementation 'com.google.android.gms:play-services-cast:22.1.0'
    playImplementation 'com.google.android.gms:play-services-cast-framework:22.1.0'
    playImplementation 'com.google.android.gms:play-services-safetynet:18.1.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.10.1'

    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0'

    //noinspection GradleDependency
    androidTestImplementation("com.squareup.okhttp3:mockwebserver:3.12.13")
    androidTestImplementation("com.github.YarikSOffice:lingver:1.3.0")
    androidTestUtil 'androidx.test:orchestrator:1.5.1'
}


// https://stackoverflow.com/questions/24649240/build-release-apk-with-customize-name-format-in-android-studio
// https://stackoverflow.com/questions/32092665/resolve-application-label-for-each-build-type/32220436#32220436
// https://stackoverflow.com/questions/18332474/how-to-set-versionname-in-apk-filename-using-gradle

// Gradle 3.0
// https://stackoverflow.com/questions/44800028/change-apk-filename-in-gradle-failed-with-gradle3-0-0-alpha4

android.applicationVariants.all { variant ->

    variant.outputs.all { output ->

        // get app_name field from defaultConfig
        def appName = variant.mergedFlavor.resValues.get('string/app_name_untranslated').getValue()
        appName = "${appName}"

        // concat new App name with each flavor's name

        variant.productFlavors.each { flavor ->
            appName += "-${flavor.name}"
        }

        // optionally add buildType name
        appName += "-${variant.buildType.name}"

        // your requirement: if buildType == debug, add DEV to App name
        if (variant.buildType.name == "debug") {
            appName += "-DEV"
        }

        // if you want, you can set a new resValue
        // variant.resValue 'string', 'app_name', appName

        /*
         * Gets the version name from the latest Git tag
         */

        def gitHash = { ->
            def stdout = new ByteArrayOutputStream()
            try {
                exec {
                    commandLine 'git', 'rev-parse', '--short', 'HEAD'
                    standardOutput = stdout
                }
                return stdout.toString().trim()
            } catch (Exception ignored) {
                return "No commit hash"
            }
        }

        resValue 'string', 'GIT_HASH', gitHash()

        def getBuildDate = {
            new Date().format('yyyy-MM-dd HH:mm:ss')
        }

        resValue 'string', 'BUILD_DATE', getBuildDate()

        def finalName = appName

        if (!versionName.isEmpty()) {
            finalName += "-" + versionName
        }

        finalName += "-" + gitHash() + ".apk"
        // outputFileName = "${variant.name}-${variant.versionName}.apk"
        outputFileName = finalName
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '27.1.1'
            }
        }
    }
}